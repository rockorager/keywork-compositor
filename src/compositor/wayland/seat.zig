//! Wayland seat global, input resources, and capability boundary.

const Self = @This();

const std = @import("std");
const wayland = @import("wayland");
const render = @import("../render/types.zig");
const Surface = @import("surface.zig");

const wl = wayland.server.wl;

allocator: std.mem.Allocator,
io: std.Io,
display: *wl.Server,
global: *wl.Global,
global_removed: bool,
name_value: [:0]const u8,
surface_store: *Surface.Store,
seat_resources: std.ArrayList(*wl.Seat),
seat_resource_listener: ?SeatResourceListener,
keyboard_resources: std.ArrayList(KeyboardResource),
pointer_resources: std.ArrayList(PointerResource),
touch_resources: std.ArrayList(TouchResource),
next_pointer_resource_generation: u64,
next_touch_resource_generation: u64,
// Input objects created before a capability is re-added must remain inert.
keyboard_capability_generation: u64,
pointer_capability_generation: u64,
touch_capability_generation: u64,
keyboard_ever_available: bool,
pointer_ever_available: bool,
touch_ever_available: bool,
keyboard_available: bool,
virtual_keyboard_count: usize,
pointer_available: bool,
touch_available: bool,
keymap: ?Keymap,
repeat_info: RepeatInfo,
keyboard_grab: ?KeyboardGrab,
repaint_listener: ?RepaintListener,
keyboard_focus_listeners: std.ArrayList(KeyboardFocusListener),
parent_focused: bool,
focus: ?Surface.Id,
pointer_focus: ?PointerFocus,
pointer_position: ?PointerPosition,
touch_points: std.ArrayList(TouchPoint),
touch_frame_resources: std.ArrayList(*wl.Touch),
latest_pointer_enter: ?UserAction,
active_cursor: ?ActiveCursor,
default_cursor: ?CursorImage,
cursor_controller: ?CursorController,
drag_cursor_client: ?*wl.Client,
cursor_surface_count: usize,
pressed_pointer_buttons: std.ArrayList(PressedPointerButton),
pressed_keys: std.ArrayList(u32),
grabbed_keys: std.ArrayList(GrabbedKey),
modifiers: Modifiers,
last_user_action: ?UserAction,
recent_user_actions: [user_action_history_capacity]UserAction,
recent_user_action_count: usize,
next_user_action: usize,

const user_action_history_capacity = 32;

const Keymap = struct {
    format: wl.Keyboard.KeymapFormat,
    file: std.Io.File,
    size: u32,
};

const RepeatInfo = struct {
    rate: i32 = 0,
    delay: i32 = 0,
};

const Modifiers = struct {
    depressed: u32 = 0,
    latched: u32 = 0,
    locked: u32 = 0,
    group: u32 = 0,
};

const GrabbedKey = struct {
    key: u32,
    token: u64,
};

const UserAction = struct {
    client: *wl.Client,
    serial: u32,
};

const PointerPosition = struct {
    x: f64,
    y: f64,
};

const TouchPoint = struct {
    id: i32,
    target: ?Target,

    const Target = struct {
        surface_id: Surface.Id,
        client: *wl.Client,
        offset_x: f64,
        offset_y: f64,
        max_resource_generation: u64,
    };
};

const TouchResource = struct {
    resource: *wl.Touch,
    generation: u64,
    capability_generation: u64,
};

const KeyboardResource = struct {
    resource: *wl.Keyboard,
    capability_generation: u64,
};

const PointerResource = struct {
    resource: *wl.Pointer,
    generation: u64,
    capability_generation: u64,
};

const PressedPointerButton = struct {
    button: u32,
    client: *wl.Client,
    surface_id: Surface.Id,
    serial: u32,
};

const SurfaceCursor = struct {
    surface_id: Surface.Id,
    hotspot_x: i32,
    hotspot_y: i32,
};

const ActiveCursor = union(enum) {
    surface: SurfaceCursor,
    shape: ShapeCursor,
};

const CursorController = struct {
    client: *wl.Client,
    cursor: ?ActiveCursor,
    configured: bool,
};

pub const PointerFocus = struct {
    surface_id: Surface.Id,
    x: f64,
    y: f64,
};

pub const PointerHandle = struct {
    resource: *wl.Pointer,
    generation: u64,
};

pub const PointerBinding = struct {
    seat: *Self,
    generation: u64,

    pub fn isActive(self: PointerBinding) bool {
        for (self.seat.pointer_resources.items) |entry| {
            if (entry.generation == self.generation) {
                return self.seat.pointerResourceActive(entry);
            }
        }
        return false;
    }
};

pub const ShapeCursor = struct {
    client: *wl.Client,
    buffer: render.PixelBuffer,
    hotspot_x: i32,
    hotspot_y: i32,
};

pub const CursorImage = struct {
    buffer: render.PixelBuffer,
    hotspot_x: i32,
    hotspot_y: i32,
};

pub const CursorInfo = union(enum) {
    surface: struct {
        surface_id: Surface.Id,
        x: i32,
        y: i32,
    },
    shape: struct {
        buffer: render.PixelBuffer,
        x: i32,
        y: i32,
    },
};

pub const RepaintListener = struct {
    context: *anyopaque,
    request: *const fn (*anyopaque) void,
    cursor_moved: *const fn (*anyopaque, CursorInfo, CursorInfo) void,
};

pub const KeyboardFocusListener = struct {
    context: *anyopaque,
    changed: *const fn (*anyopaque, ?*wl.Client) void,
};

pub const SeatResourceListener = struct {
    context: *anyopaque,
    changed: *const fn (*anyopaque, usize) void,
};

pub const KeyboardGrab = struct {
    context: *anyopaque,
    token: u64,
    surface: ?Surface.Id = null,
    cancel: ?*const fn (*anyopaque) void = null,
    keymap: *const fn (*anyopaque, wl.Keyboard.KeymapFormat, std.posix.fd_t, u32) void,
    key: *const fn (*anyopaque, u32, u32, u32, wl.Keyboard.KeyState) void,
    modifiers: *const fn (*anyopaque, u32, u32, u32, u32) void,
    repeat_info: *const fn (*anyopaque, i32, i32) void,
};

pub fn init(
    self: *Self,
    allocator: std.mem.Allocator,
    io: std.Io,
    display: *wl.Server,
    seat_name: [:0]const u8,
    surface_store: *Surface.Store,
) !void {
    self.* = .{
        .allocator = allocator,
        .io = io,
        .display = display,
        .global = undefined,
        .global_removed = false,
        .name_value = seat_name,
        .surface_store = surface_store,
        .seat_resources = .empty,
        .seat_resource_listener = null,
        .keyboard_resources = .empty,
        .pointer_resources = .empty,
        .touch_resources = .empty,
        .next_pointer_resource_generation = 0,
        .next_touch_resource_generation = 0,
        .keyboard_capability_generation = 0,
        .pointer_capability_generation = 0,
        .touch_capability_generation = 0,
        .keyboard_ever_available = false,
        .pointer_ever_available = false,
        .touch_ever_available = false,
        .keyboard_available = false,
        .virtual_keyboard_count = 0,
        .pointer_available = false,
        .touch_available = false,
        .keymap = null,
        .repeat_info = .{},
        .keyboard_grab = null,
        .repaint_listener = null,
        .keyboard_focus_listeners = .empty,
        .parent_focused = false,
        .focus = null,
        .pointer_focus = null,
        .pointer_position = null,
        .touch_points = .empty,
        .touch_frame_resources = .empty,
        .latest_pointer_enter = null,
        .active_cursor = null,
        .default_cursor = null,
        .cursor_controller = null,
        .drag_cursor_client = null,
        .cursor_surface_count = 0,
        .pressed_pointer_buttons = .empty,
        .pressed_keys = .empty,
        .grabbed_keys = .empty,
        .modifiers = .{},
        .last_user_action = null,
        .recent_user_actions = undefined,
        .recent_user_action_count = 0,
        .next_user_action = 0,
    };
    errdefer self.seat_resources.deinit(allocator);
    errdefer self.keyboard_resources.deinit(allocator);
    errdefer self.pointer_resources.deinit(allocator);
    errdefer self.touch_resources.deinit(allocator);
    errdefer self.touch_points.deinit(allocator);
    errdefer self.touch_frame_resources.deinit(allocator);
    errdefer self.pressed_pointer_buttons.deinit(allocator);
    errdefer self.pressed_keys.deinit(allocator);
    errdefer self.grabbed_keys.deinit(allocator);
    errdefer self.keyboard_focus_listeners.deinit(allocator);
    self.global = try wl.Global.create(display, wl.Seat, 10, *Self, self, bind);
}

pub fn deinit(self: *Self) void {
    std.debug.assert(self.seat_resources.items.len == 0);
    std.debug.assert(self.keyboard_resources.items.len == 0);
    std.debug.assert(self.pointer_resources.items.len == 0);
    std.debug.assert(self.touch_resources.items.len == 0);
    std.debug.assert(self.cursor_surface_count == 0);
    std.debug.assert(self.virtual_keyboard_count == 0);
    std.debug.assert(self.keyboard_grab == null);
    std.debug.assert(self.repaint_listener == null);
    std.debug.assert(self.keyboard_focus_listeners.items.len == 0);
    std.debug.assert(self.seat_resource_listener == null);
    self.global.destroy();
    if (self.keymap) |keymap| keymap.file.close(self.io);
    self.keyboard_focus_listeners.deinit(self.allocator);
    self.grabbed_keys.deinit(self.allocator);
    self.pressed_keys.deinit(self.allocator);
    self.pressed_pointer_buttons.deinit(self.allocator);
    self.touch_frame_resources.deinit(self.allocator);
    self.touch_points.deinit(self.allocator);
    self.touch_resources.deinit(self.allocator);
    self.pointer_resources.deinit(self.allocator);
    self.keyboard_resources.deinit(self.allocator);
    self.seat_resources.deinit(self.allocator);
    self.* = undefined;
}

pub fn globalName(self: *const Self, client: *const wl.Client) u32 {
    std.debug.assert(!self.global_removed);
    return self.global.getName(client);
}

/// Stop advertising this seat while keeping existing client resources alive.
pub fn removeGlobal(self: *Self) void {
    std.debug.assert(!self.global_removed);
    self.global.remove();
    self.global_removed = true;
}

pub fn name(self: *const Self) [:0]const u8 {
    return self.name_value;
}

pub fn ownsResource(self: *Self, resource: *wl.Seat) bool {
    return resource.getUserData() == @as(?*anyopaque, @ptrCast(self));
}

pub fn fromResource(resource: *wl.Seat) *Self {
    const data = resource.getUserData() orelse unreachable;
    return @ptrCast(@alignCast(data));
}

pub fn setSeatResourceListener(self: *Self, listener: SeatResourceListener) void {
    std.debug.assert(self.seat_resource_listener == null);
    self.seat_resource_listener = listener;
}

pub fn clearSeatResourceListener(self: *Self) void {
    std.debug.assert(self.seat_resource_listener != null);
    self.seat_resource_listener = null;
}

pub fn pointerBinding(resource: *wl.Pointer) ?PointerBinding {
    const data = resource.getUserData() orelse return null;
    const self: *Self = @ptrCast(@alignCast(data));
    const handle = self.pointerHandle(resource) orelse return null;
    if (!self.pointerHandleIsActive(handle)) return null;
    return .{ .seat = self, .generation = handle.generation };
}

pub fn pointerHandle(self: *const Self, resource: *wl.Pointer) ?PointerHandle {
    for (self.pointer_resources.items) |entry| {
        if (entry.resource == resource) return .{
            .resource = resource,
            .generation = entry.generation,
        };
    }
    return null;
}

pub fn pointerHandleIsActive(self: *const Self, handle: PointerHandle) bool {
    for (self.pointer_resources.items) |entry| {
        if (entry.resource == handle.resource and entry.generation == handle.generation) {
            return self.pointerResourceActive(entry);
        }
    }
    return false;
}

pub fn setRepaintListener(self: *Self, listener: RepaintListener) void {
    std.debug.assert(self.repaint_listener == null);
    self.repaint_listener = listener;
}

pub fn clearRepaintListener(self: *Self) void {
    std.debug.assert(self.repaint_listener != null);
    self.repaint_listener = null;
}

pub fn addKeyboardFocusListener(
    self: *Self,
    listener: KeyboardFocusListener,
) error{OutOfMemory}!void {
    for (self.keyboard_focus_listeners.items) |existing| {
        std.debug.assert(existing.context != listener.context);
    }
    try self.keyboard_focus_listeners.append(self.allocator, listener);
}

pub fn removeKeyboardFocusListener(self: *Self, context: *anyopaque) void {
    for (self.keyboard_focus_listeners.items, 0..) |listener, index| {
        if (listener.context != context) continue;
        _ = self.keyboard_focus_listeners.orderedRemove(index);
        return;
    }
    unreachable;
}

pub fn setKeyboardGrab(self: *Self, grab: KeyboardGrab) void {
    if (self.keyboard_grab) |active| {
        const cancel = active.cancel orelse unreachable;
        cancel(active.context);
    }
    std.debug.assert(self.installKeyboardGrab(grab));
}

pub fn trySetKeyboardGrab(self: *Self, grab: KeyboardGrab) bool {
    if (self.keyboard_grab != null) return false;
    return self.installKeyboardGrab(grab);
}

fn installKeyboardGrab(self: *Self, grab: KeyboardGrab) bool {
    std.debug.assert(self.keyboard_grab == null);
    if (grab.surface) |surface_id| {
        if (Surface.resourceFor(self.surface_store, surface_id) == null) return false;
        if (self.parent_focused) self.sendLeave();
    }
    self.keyboard_grab = grab;
    if (grab.surface != null) {
        if (self.parent_focused and self.keymap != null) self.sendEnter();
        self.notifyKeyboardFocus();
    } else {
        if (self.keymap) |keymap| {
            grab.keymap(grab.context, keymap.format, keymap.file.handle, keymap.size);
        }
        grab.repeat_info(grab.context, self.repeat_info.rate, self.repeat_info.delay);
        grab.modifiers(
            grab.context,
            self.modifiers.depressed,
            self.modifiers.latched,
            self.modifiers.locked,
            self.modifiers.group,
        );
    }
    return true;
}

pub fn clearKeyboardGrab(self: *Self, context: *anyopaque, restore_focus: bool) void {
    const grab = self.keyboard_grab orelse unreachable;
    std.debug.assert(grab.context == context);
    if (grab.surface != null and self.parent_focused) self.sendLeave();
    self.keyboard_grab = null;
    if (!restore_focus) return;
    if (grab.surface != null) {
        self.notifyKeyboardFocus();
        if (self.parent_focused and self.keymap != null) self.sendEnter();
        return;
    }
    const surface = self.focusedSurface() orelse return;
    if (!self.parent_focused or self.keymap == null) return;
    const serial = self.display.nextSerial();
    for (self.keyboard_resources.items) |entry| {
        if (!self.keyboardResourceActive(entry)) continue;
        if (entry.resource.getClient() == surface.getClient()) {
            self.sendModifiers(entry.resource, serial);
        }
    }
}

pub fn acceptsUserActionSerial(
    self: *Self,
    resource: *wl.Seat,
    client: *wl.Client,
    serial: u32,
) bool {
    if (!self.ownsResource(resource)) return false;
    return self.acceptsClientUserActionSerial(client, serial);
}

pub fn acceptsSelectionSerial(self: *Self, client: *wl.Client, serial: u32) bool {
    for (self.recent_user_actions[0..self.recent_user_action_count]) |action| {
        if (action.client == client and action.serial == serial) return true;
    }
    return false;
}

pub fn acceptsActivationSerial(
    self: *Self,
    resource: *wl.Seat,
    client: *wl.Client,
    serial: u32,
) bool {
    if (!self.ownsResource(resource)) return false;
    if (self.acceptsSelectionSerial(client, serial)) return true;
    const pointer_enter = self.latest_pointer_enter orelse return false;
    return pointer_enter.client == client and pointer_enter.serial == serial;
}

pub fn activationSurfaceFocused(self: *const Self, surface_id: Surface.Id) bool {
    if (self.focus) |focus| {
        if (std.meta.eql(focus, surface_id)) return true;
    }
    if (self.pointer_focus) |focus| {
        if (std.meta.eql(focus.surface_id, surface_id)) return true;
    }
    for (self.touch_points.items) |point| {
        const target = point.target orelse continue;
        if (std.meta.eql(target.surface_id, surface_id)) return true;
    }
    return false;
}

pub fn acceptsClientUserActionSerial(self: *const Self, client: *wl.Client, serial: u32) bool {
    const action = self.last_user_action orelse return false;
    return action.client == client and action.serial == serial;
}

pub fn acceptsPointerGrabSerial(
    self: *const Self,
    client: *wl.Client,
    surface_id: Surface.Id,
    serial: u32,
) bool {
    for (self.pressed_pointer_buttons.items) |press| {
        if (press.client == client and press.serial == serial and
            std.meta.eql(press.surface_id, surface_id)) return true;
    }
    return false;
}

pub fn hasPressedPointerButton(self: *const Self, button: u32) bool {
    for (self.pressed_pointer_buttons.items) |press| {
        if (press.button == button) return true;
    }
    return false;
}

pub fn hasPressedPointerButtons(self: *const Self) bool {
    return self.pressed_pointer_buttons.items.len != 0;
}

pub fn hasPressedPointerButtonForSurface(
    self: *const Self,
    button: u32,
    surface_id: Surface.Id,
) bool {
    for (self.pressed_pointer_buttons.items) |press| {
        if (press.button == button and std.meta.eql(press.surface_id, surface_id)) return true;
    }
    return false;
}

pub fn forgetPressedPointerButton(self: *Self, button: u32) void {
    for (self.pressed_pointer_buttons.items, 0..) |press, index| {
        if (press.button != button) continue;
        _ = self.pressed_pointer_buttons.orderedRemove(index);
        return;
    }
}

pub fn serialIsOlder(candidate: u32, current: u32) bool {
    return candidate -% current > std.math.maxInt(u32) / 2;
}

pub fn pointerFocusedSurface(self: *const Self) ?Surface.Id {
    const focus = self.pointer_focus orelse return null;
    return focus.surface_id;
}

pub fn pointerFocus(self: *const Self) ?PointerFocus {
    return self.pointer_focus;
}

pub fn pointerFocusedClient(self: *const Self) ?*wl.Client {
    const focus = self.pointer_focus orelse return null;
    const resource = Surface.resourceFor(self.surface_store, focus.surface_id) orelse return null;
    return resource.getClient();
}

pub fn pointerFocusedResource(self: *Self) ?*wl.Surface {
    return self.pointerSurface();
}

pub fn pointerPosition(self: *const Self) ?struct { x: f64, y: f64 } {
    const position = self.pointer_position orelse return null;
    return .{ .x = position.x, .y = position.y };
}

pub fn effectiveModifiers(self: *const Self) u32 {
    return (self.modifiers.depressed | self.modifiers.latched) & 0xed;
}

/// Set the client allowed to own the cursor while pointer focus is absent.
/// This is also the generic ownership query point for cursor-shape protocols.
pub fn setUnfocusedCursorController(self: *Self, client: ?*wl.Client) void {
    if (client == null) {
        self.cursor_controller = null;
    } else if (self.cursor_controller == null or self.cursor_controller.?.client != client.?) {
        self.cursor_controller = .{ .client = client.?, .cursor = null, .configured = false };
    }
    if (self.pointer_focus == null) self.restoreControllerCursor();
}

pub fn isUnfocusedCursorController(self: *const Self, client: *wl.Client) bool {
    return if (self.cursor_controller) |controller| controller.client == client else false;
}

pub fn setDragCursorController(self: *Self, client: ?*wl.Client) void {
    self.drag_cursor_client = client;
    if (client == null) self.restoreControllerCursor();
}

pub fn suppressPointerFocus(self: *Self, suppress: bool) void {
    if (suppress) self.updatePointerFocus(null, null);
}

pub fn restoreUnfocusedCursor(self: *Self) void {
    self.restoreControllerCursor();
}

pub fn keyboardFocusedClient(self: *Self) ?*wl.Client {
    if (!self.hasKeyboardCapability()) return null;
    if (!self.parent_focused or self.keymap == null) return null;
    const surface = self.keyboardDeliverySurface() orelse return null;
    return surface.getClient();
}

pub fn keyboardFocusedSurface(self: *const Self) ?Surface.Id {
    if (!self.hasKeyboardCapability()) return null;
    if (!self.parent_focused or self.keymap == null) return null;
    const focus = self.keyboardDeliverySurfaceId() orelse return null;
    if (Surface.resourceFor(self.surface_store, focus) == null) return null;
    return focus;
}

pub fn cursorInfo(self: *const Self) ?CursorInfo {
    const position = self.pointer_position orelse return null;
    const cursor = self.active_cursor orelse {
        if (self.pointer_focus != null or self.drag_cursor_client != null) return null;
        if (self.cursor_controller) |controller| if (controller.configured) return null;
        const fallback = self.default_cursor orelse return null;
        return .{ .shape = .{
            .buffer = fallback.buffer,
            .x = cursorCoordinate(position.x, fallback.hotspot_x),
            .y = cursorCoordinate(position.y, fallback.hotspot_y),
        } };
    };
    return switch (cursor) {
        .surface => |surface| .{ .surface = .{
            .surface_id = surface.surface_id,
            .x = cursorCoordinate(position.x, surface.hotspot_x),
            .y = cursorCoordinate(position.y, surface.hotspot_y),
        } },
        .shape => |shape| .{ .shape = .{
            .buffer = shape.buffer,
            .x = cursorCoordinate(position.x, shape.hotspot_x),
            .y = cursorCoordinate(position.y, shape.hotspot_y),
        } },
    };
}

pub fn setDefaultCursor(self: *Self, cursor: ?CursorImage) void {
    self.default_cursor = cursor;
    self.requestRepaint();
}

pub fn setKeyboardAvailable(self: *Self, available: bool) void {
    if (self.keyboard_available == available) return;
    const old_capability = self.hasKeyboardCapability();
    if (!available and self.virtual_keyboard_count == 0) self.parentKeyboardLeave();
    self.keyboard_available = available;
    const new_capability = self.hasKeyboardCapability();
    if (!old_capability and new_capability) {
        beginCapabilityGeneration(
            &self.keyboard_capability_generation,
            &self.keyboard_ever_available,
        );
    }
    if (old_capability != new_capability) self.broadcastCapabilities();
}

pub fn addVirtualKeyboard(self: *Self) void {
    const old_capability = self.hasKeyboardCapability();
    self.virtual_keyboard_count = std.math.add(usize, self.virtual_keyboard_count, 1) catch
        unreachable;
    const new_capability = self.hasKeyboardCapability();
    if (!old_capability and new_capability) {
        beginCapabilityGeneration(
            &self.keyboard_capability_generation,
            &self.keyboard_ever_available,
        );
    }
    if (old_capability == new_capability) return;
    self.broadcastCapabilities();
    if (self.parent_focused) {
        self.notifyKeyboardFocus();
        self.sendEnter();
    }
}

pub fn removeVirtualKeyboard(self: *Self) void {
    std.debug.assert(self.virtual_keyboard_count > 0);
    const old_capability = self.hasKeyboardCapability();
    if (old_capability and !self.keyboard_available and
        self.virtual_keyboard_count == 1 and self.parent_focused) self.sendLeave();
    self.virtual_keyboard_count -= 1;
    const new_capability = self.hasKeyboardCapability();
    if (old_capability == new_capability) return;
    self.broadcastCapabilities();
    self.notifyKeyboardFocus();
}

pub fn setPointerAvailable(self: *Self, available: bool) void {
    if (self.pointer_available == available) return;
    if (!available) {
        self.pointerLeave();
        self.pressed_pointer_buttons.clearRetainingCapacity();
    }
    self.pointer_available = available;
    if (available) {
        beginCapabilityGeneration(
            &self.pointer_capability_generation,
            &self.pointer_ever_available,
        );
    }
    self.broadcastCapabilities();
}

pub fn setTouchAvailable(self: *Self, available: bool) void {
    if (self.touch_available == available) return;
    if (!available) self.touchCancel();
    self.touch_available = available;
    if (available) {
        beginCapabilityGeneration(
            &self.touch_capability_generation,
            &self.touch_ever_available,
        );
    }
    self.broadcastCapabilities();
}

pub fn setKeymap(
    self: *Self,
    format: wl.Keyboard.KeymapFormat,
    fd: std.posix.fd_t,
    size: u32,
) void {
    const old_focus = self.keyboardFocusedClient();
    const old_capability = self.hasKeyboardCapability();
    if (self.keymap) |keymap| keymap.file.close(self.io);
    self.keymap = .{
        .format = format,
        .file = .{ .handle = fd, .flags = .{ .nonblocking = false } },
        .size = size,
    };
    const new_capability = self.hasKeyboardCapability();
    if (!old_capability and new_capability) {
        beginCapabilityGeneration(
            &self.keyboard_capability_generation,
            &self.keyboard_ever_available,
        );
    }
    if (old_capability != new_capability) self.broadcastCapabilities();
    for (self.keyboard_resources.items) |entry| {
        if (!self.keyboardResourceActive(entry)) continue;
        self.sendKeymap(entry.resource);
        self.sendRepeatInfo(entry.resource);
    }
    if (self.keyboard_grab) |grab| {
        if (grab.surface == null) grab.keymap(grab.context, format, fd, size);
    }
    if (old_focus == null and self.keyboardFocusedClient() != null) {
        self.notifyKeyboardFocus();
        self.sendEnter();
    }
}

pub fn setRepeatInfo(self: *Self, rate: i32, delay: i32) void {
    std.debug.assert(rate >= 0 and delay >= 0);
    self.repeat_info = .{ .rate = rate, .delay = delay };
    for (self.keyboard_resources.items) |entry| {
        if (self.keyboardResourceActive(entry)) self.sendRepeatInfo(entry.resource);
    }
    if (self.keyboard_grab) |grab| {
        if (grab.surface == null) grab.repeat_info(grab.context, rate, delay);
    }
}

pub fn setKeyboardFocus(self: *Self, focus: ?Surface.Id) void {
    if (std.meta.eql(self.focus, focus)) return;
    if (self.keyboard_grab) |grab| if (grab.surface != null) {
        self.focus = focus;
        return;
    };
    if (self.parent_focused) self.sendLeave();
    self.focus = focus;
    if (self.parent_focused) {
        self.notifyKeyboardFocus();
        self.sendEnter();
    }
}

pub fn parentKeyboardEnter(self: *Self, pressed_keys: []const u32) error{OutOfMemory}!void {
    self.pressed_keys.clearRetainingCapacity();
    self.grabbed_keys.clearRetainingCapacity();
    try self.pressed_keys.appendSlice(self.allocator, pressed_keys);
    if (self.parent_focused) return;
    self.parent_focused = true;
    self.notifyKeyboardFocus();
    self.sendEnter();
}

pub fn ensureParentKeyboardEnter(self: *Self) void {
    if (self.parent_focused) return;
    self.parent_focused = true;
    self.notifyKeyboardFocus();
    self.sendEnter();
}

pub fn parentKeyboardLeave(self: *Self) void {
    if (self.parent_focused) self.sendLeave();
    self.parent_focused = false;
    self.notifyKeyboardFocus();
    self.pressed_keys.clearRetainingCapacity();
    self.grabbed_keys.clearRetainingCapacity();
}

pub fn key(
    self: *Self,
    time: u32,
    key_code: u32,
    state: wl.Keyboard.KeyState,
) error{OutOfMemory}!void {
    try self.keyWithGrab(time, key_code, state, true);
}

pub fn virtualKey(
    self: *Self,
    time: u32,
    key_code: u32,
    state: wl.Keyboard.KeyState,
) error{OutOfMemory}!void {
    try self.keyWithGrab(time, key_code, state, false);
}

fn keyWithGrab(
    self: *Self,
    time: u32,
    key_code: u32,
    state: wl.Keyboard.KeyState,
    allow_grab: bool,
) error{OutOfMemory}!void {
    var route_to_grab: ?u64 = null;
    switch (state) {
        .pressed => {
            for (self.pressed_keys.items) |pressed| {
                if (pressed == key_code) return;
            }
            try self.pressed_keys.append(self.allocator, key_code);
            errdefer _ = self.pressed_keys.pop();
            if (allow_grab) {
                if (self.keyboard_grab) |grab| {
                    try self.grabbed_keys.append(self.allocator, .{
                        .key = key_code,
                        .token = grab.token,
                    });
                    route_to_grab = grab.token;
                }
            }
        },
        .released => {
            for (self.pressed_keys.items, 0..) |pressed, index| {
                if (pressed != key_code) continue;
                _ = self.pressed_keys.orderedRemove(index);
                break;
            } else return;
            if (allow_grab) {
                for (self.grabbed_keys.items, 0..) |grabbed, index| {
                    if (grabbed.key != key_code) continue;
                    route_to_grab = grabbed.token;
                    _ = self.grabbed_keys.orderedRemove(index);
                    break;
                }
            }
        },
        .repeated => for (self.grabbed_keys.items) |grabbed| {
            if (grabbed.key == key_code) {
                route_to_grab = grabbed.token;
                break;
            }
        },
        else => return,
    }

    if (!self.parent_focused or self.keymap == null) return;
    if (route_to_grab) |token| {
        const grab = self.keyboard_grab orelse return;
        if (grab.token != token) return;
        if (grab.surface) |surface_id| {
            const surface = Surface.resourceFor(self.surface_store, surface_id) orelse return;
            const serial = self.display.nextSerial();
            if (state == .pressed)
                self.recordUserAction(surface.getClient(), serial)
            else
                self.recordSelectionSerial(surface.getClient(), serial);
            for (self.keyboard_resources.items) |entry| {
                if (!self.keyboardResourceActive(entry)) continue;
                const resource = entry.resource;
                if (resource.getClient() != surface.getClient()) continue;
                if (state == .repeated and resource.getVersion() < 10) continue;
                resource.sendKey(serial, time, key_code, state);
            }
            return;
        }
        if (state == .repeated) return;
        grab.key(grab.context, self.display.nextSerial(), time, key_code, state);
        return;
    }
    const surface = self.focusedSurface() orelse return;
    const serial = self.display.nextSerial();
    if (state == .pressed)
        self.recordUserAction(surface.getClient(), serial)
    else
        self.recordSelectionSerial(surface.getClient(), serial);
    for (self.keyboard_resources.items) |entry| {
        if (!self.keyboardResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() != surface.getClient()) continue;
        if (state == .repeated and resource.getVersion() < 10) continue;
        resource.sendKey(serial, time, key_code, state);
    }
}

pub fn setModifiers(
    self: *Self,
    depressed: u32,
    latched: u32,
    locked: u32,
    group: u32,
) void {
    self.setModifiersWithGrab(depressed, latched, locked, group, true);
}

pub fn setVirtualModifiers(
    self: *Self,
    depressed: u32,
    latched: u32,
    locked: u32,
    group: u32,
) void {
    self.setModifiersWithGrab(depressed, latched, locked, group, false);
}

fn setModifiersWithGrab(
    self: *Self,
    depressed: u32,
    latched: u32,
    locked: u32,
    group: u32,
    allow_grab: bool,
) void {
    self.modifiers = .{
        .depressed = depressed,
        .latched = latched,
        .locked = locked,
        .group = group,
    };
    if (allow_grab) {
        if (self.keyboard_grab) |grab| {
            if (grab.surface) |surface_id| {
                const surface = Surface.resourceFor(self.surface_store, surface_id) orelse return;
                const serial = self.display.nextSerial();
                for (self.keyboard_resources.items) |entry| {
                    if (!self.keyboardResourceActive(entry)) continue;
                    if (entry.resource.getClient() == surface.getClient()) {
                        self.sendModifiers(entry.resource, serial);
                    }
                }
                return;
            }
            grab.modifiers(grab.context, depressed, latched, locked, group);
            return;
        }
    }
    const surface = self.focusedSurface() orelse return;
    if (!self.parent_focused or self.keymap == null) return;
    const serial = self.display.nextSerial();
    for (self.keyboard_resources.items) |entry| {
        if (!self.keyboardResourceActive(entry)) continue;
        if (entry.resource.getClient() == surface.getClient()) {
            self.sendModifiers(entry.resource, serial);
        }
    }
}

pub fn pointerEnter(self: *Self, x: f64, y: f64, focus: ?PointerFocus) void {
    self.setPointerPosition(x, y);
    self.updatePointerFocus(focus, null);
}

pub fn pointerMotion(self: *Self, time: u32, x: f64, y: f64, focus: ?PointerFocus) void {
    self.setPointerPosition(x, y);
    self.updatePointerFocus(focus, time);
}

pub fn pointerLeave(self: *Self) void {
    const fallback_visible = self.active_cursor == null and self.cursorInfo() != null;
    self.clearCursor();
    self.sendPointerLeave();
    self.pointer_focus = null;
    self.pointer_position = null;
    self.latest_pointer_enter = null;
    if (fallback_visible) self.requestRepaint();
}

pub fn pointerButton(
    self: *Self,
    time: u32,
    button: u32,
    state: wl.Pointer.ButtonState,
) error{OutOfMemory}!bool {
    switch (state) {
        .pressed => {
            for (self.pressed_pointer_buttons.items) |press| {
                if (press.button == button) return false;
            }
            const surface = self.pointerSurface() orelse return false;
            const serial = self.display.nextSerial();
            try self.pressed_pointer_buttons.append(self.allocator, .{
                .button = button,
                .client = surface.getClient(),
                .surface_id = self.pointer_focus.?.surface_id,
                .serial = serial,
            });
            self.recordUserAction(surface.getClient(), serial);
            for (self.pointer_resources.items) |entry| {
                if (!self.pointerResourceActive(entry)) continue;
                const resource = entry.resource;
                if (resource.getClient() == surface.getClient()) {
                    resource.sendButton(serial, time, button, state);
                }
            }
            return false;
        },
        .released => {
            for (self.pressed_pointer_buttons.items, 0..) |press, index| {
                if (press.button != button) continue;
                _ = self.pressed_pointer_buttons.orderedRemove(index);
                break;
            } else return false;
        },
        else => return false,
    }

    const grab_ended = self.pressed_pointer_buttons.items.len == 0;
    const surface = self.pointerSurface() orelse return grab_ended;
    const serial = self.display.nextSerial();
    self.recordSelectionSerial(surface.getClient(), serial);
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient()) {
            resource.sendButton(serial, time, button, state);
        }
    }
    return grab_ended;
}

pub fn pointerAxis(self: *Self, time: u32, axis: wl.Pointer.Axis, value: wl.Fixed) void {
    const surface = self.pointerSurface() orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient()) resource.sendAxis(time, axis, value);
    }
}

pub fn pointerFrame(self: *Self) void {
    const surface = self.pointerSurface() orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient() and
            resource.getVersion() >= wl.Pointer.frame_since_version)
        {
            resource.sendFrame();
        }
    }
}

pub fn pointerAxisSource(self: *Self, source: wl.Pointer.AxisSource) void {
    const surface = self.pointerSurface() orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient() and
            resource.getVersion() >= wl.Pointer.axis_source_since_version)
        {
            resource.sendAxisSource(source);
        }
    }
}

pub fn pointerAxisStop(self: *Self, time: u32, axis: wl.Pointer.Axis) void {
    const surface = self.pointerSurface() orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient() and
            resource.getVersion() >= wl.Pointer.axis_stop_since_version)
        {
            resource.sendAxisStop(time, axis);
        }
    }
}

pub fn pointerAxisDiscrete(self: *Self, axis: wl.Pointer.Axis, discrete: i32) void {
    const surface = self.pointerSurface() orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient() and
            resource.getVersion() >= wl.Pointer.axis_discrete_since_version and
            resource.getVersion() < wl.Pointer.axis_value120_since_version)
        {
            resource.sendAxisDiscrete(axis, discrete);
        }
    }
}

pub fn pointerAxisValue120(self: *Self, axis: wl.Pointer.Axis, value120: i32) void {
    const surface = self.pointerSurface() orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient() and
            resource.getVersion() >= wl.Pointer.axis_value120_since_version)
        {
            resource.sendAxisValue120(axis, value120);
        }
    }
}

pub fn pointerAxisRelativeDirection(
    self: *Self,
    axis: wl.Pointer.Axis,
    direction: wl.Pointer.AxisRelativeDirection,
) void {
    const surface = self.pointerSurface() orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient() and
            resource.getVersion() >= wl.Pointer.axis_relative_direction_since_version)
        {
            resource.sendAxisRelativeDirection(axis, direction);
        }
    }
}

pub fn touchDown(
    self: *Self,
    time: u32,
    id: i32,
    x: f64,
    y: f64,
    focus: ?PointerFocus,
) error{OutOfMemory}!void {
    if (!self.touch_available or self.findTouchPoint(id) != null) return;
    try self.touch_points.ensureUnusedCapacity(self.allocator, 1);

    const target: ?TouchPoint.Target = if (focus) |candidate| target: {
        const surface = Surface.resourceFor(self.surface_store, candidate.surface_id) orelse
            break :target null;
        const max_resource_generation = self.latestTouchResourceGeneration(
            surface.getClient(),
        ) orelse break :target null;
        break :target .{
            .surface_id = candidate.surface_id,
            .client = surface.getClient(),
            .offset_x = x - candidate.x,
            .offset_y = y - candidate.y,
            .max_resource_generation = max_resource_generation,
        };
    } else null;
    self.touch_points.appendAssumeCapacity(.{ .id = id, .target = target });

    const destination = target orelse return;
    const surface = Surface.resourceFor(self.surface_store, destination.surface_id) orelse return;
    const serial = self.display.nextSerial();
    self.recordUserAction(destination.client, serial);
    for (self.touch_resources.items) |entry| {
        if (!self.touchResourceActive(entry)) continue;
        const resource = entry.resource;
        if (touchResourceInSequence(entry.generation, destination.max_resource_generation) and
            resource.getClient() == destination.client)
        {
            try self.markTouchFrame(resource);
            resource.sendDown(
                serial,
                time,
                surface,
                id,
                fixed(x - destination.offset_x),
                fixed(y - destination.offset_y),
            );
        }
    }
}

pub fn touchUp(self: *Self, time: u32, id: i32) error{OutOfMemory}!void {
    if (!self.touch_available) return;
    const index = self.findTouchPoint(id) orelse return;
    const point = self.touch_points.items[index];
    if (point.target) |target| {
        const serial = self.display.nextSerial();
        self.recordSelectionSerial(target.client, serial);
        for (self.touch_resources.items) |entry| {
            if (!self.touchResourceActive(entry)) continue;
            const resource = entry.resource;
            if (touchResourceInSequence(entry.generation, target.max_resource_generation) and
                resource.getClient() == target.client)
            {
                try self.markTouchFrame(resource);
                resource.sendUp(serial, time, id);
            }
        }
    }
    _ = self.touch_points.orderedRemove(index);
}

pub fn touchMotion(
    self: *Self,
    time: u32,
    id: i32,
    x: f64,
    y: f64,
) error{OutOfMemory}!void {
    if (!self.touch_available) return;
    const point = self.touchPoint(id) orelse return;
    const target = point.target orelse return;
    for (self.touch_resources.items) |entry| {
        if (!self.touchResourceActive(entry)) continue;
        const resource = entry.resource;
        if (touchResourceInSequence(entry.generation, target.max_resource_generation) and
            resource.getClient() == target.client)
        {
            try self.markTouchFrame(resource);
            resource.sendMotion(
                time,
                id,
                fixed(x - target.offset_x),
                fixed(y - target.offset_y),
            );
        }
    }
}

pub fn touchFrame(self: *Self) void {
    for (self.touch_frame_resources.items) |resource| resource.sendFrame();
    self.touch_frame_resources.clearRetainingCapacity();
}

pub fn touchCancel(self: *Self) void {
    for (self.touch_resources.items) |entry| {
        if (!self.touchResourceActive(entry)) continue;
        for (self.touch_points.items) |point| {
            const target = point.target orelse continue;
            if (!touchResourceInSequence(entry.generation, target.max_resource_generation)) continue;
            if (entry.resource.getClient() != target.client) continue;
            entry.resource.sendCancel();
            break;
        }
    }
    self.touch_points.clearRetainingCapacity();
    self.touch_frame_resources.clearRetainingCapacity();
}

pub fn touchShape(self: *Self, id: i32, major: f64, minor: f64) error{OutOfMemory}!void {
    if (!self.touch_available) return;
    const point = self.touchPoint(id) orelse return;
    const target = point.target orelse return;
    if (!self.hasTouchResourceVersion(
        target.client,
        wl.Touch.shape_since_version,
        target.max_resource_generation,
    )) return;
    for (self.touch_resources.items) |entry| {
        if (!self.touchResourceActive(entry)) continue;
        const resource = entry.resource;
        if (touchResourceInSequence(entry.generation, target.max_resource_generation) and
            resource.getClient() == target.client and
            resource.getVersion() >= wl.Touch.shape_since_version)
        {
            try self.markTouchFrame(resource);
            resource.sendShape(id, fixed(major), fixed(minor));
        }
    }
}

pub fn touchOrientation(self: *Self, id: i32, orientation: f64) error{OutOfMemory}!void {
    if (!self.touch_available) return;
    const point = self.touchPoint(id) orelse return;
    const target = point.target orelse return;
    if (!self.hasTouchResourceVersion(
        target.client,
        wl.Touch.orientation_since_version,
        target.max_resource_generation,
    )) return;
    for (self.touch_resources.items) |entry| {
        if (!self.touchResourceActive(entry)) continue;
        const resource = entry.resource;
        if (touchResourceInSequence(entry.generation, target.max_resource_generation) and
            resource.getClient() == target.client and
            resource.getVersion() >= wl.Touch.orientation_since_version)
        {
            try self.markTouchFrame(resource);
            resource.sendOrientation(id, fixed(orientation));
        }
    }
}

fn bind(client: *wl.Client, self: *Self, version: u32, id: u32) void {
    const resource = wl.Seat.create(client, version, id) catch {
        client.postNoMemory();
        return;
    };
    self.seat_resources.append(self.allocator, resource) catch {
        resource.postNoMemory();
        resource.destroy();
        return;
    };
    resource.setHandler(*Self, handleRequest, handleSeatDestroy, self);
    self.notifySeatResourceCount();
    if (version >= wl.Seat.name_since_version) resource.sendName(self.name_value);
    resource.sendCapabilities(self.capabilities());
}

fn handleRequest(resource: *wl.Seat, request: wl.Seat.Request, self: *Self) void {
    switch (request) {
        .release => resource.destroy(),
        .get_keyboard => |get| if (self.keyboard_ever_available)
            self.createKeyboard(resource, get.id)
        else
            resource.postError(.missing_capability, "seat has never had a keyboard capability"),
        .get_pointer => |get| if (self.pointer_ever_available)
            self.createPointer(resource, get.id)
        else
            resource.postError(.missing_capability, "seat has never had a pointer capability"),
        .get_touch => |get| if (self.touch_ever_available)
            self.createTouch(resource, get.id)
        else
            resource.postError(.missing_capability, "seat has never had a touch capability"),
    }
}

fn handleSeatDestroy(resource: *wl.Seat, self: *Self) void {
    for (self.seat_resources.items, 0..) |candidate, index| {
        if (candidate != resource) continue;
        _ = self.seat_resources.orderedRemove(index);
        self.notifySeatResourceCount();
        return;
    }
    unreachable;
}

fn notifySeatResourceCount(self: *Self) void {
    const listener = self.seat_resource_listener orelse return;
    // The listener may deinitialize this seat once the count reaches zero, so
    // destruction handlers must not access self after notifying it.
    listener.changed(
        listener.context,
        self.seat_resources.items.len +
            self.keyboard_resources.items.len +
            self.pointer_resources.items.len +
            self.touch_resources.items.len,
    );
}

fn createKeyboard(self: *Self, seat: *wl.Seat, id: u32) void {
    const resource = wl.Keyboard.create(seat.getClient(), seat.getVersion(), id) catch {
        seat.postNoMemory();
        return;
    };
    const entry: KeyboardResource = .{
        .resource = resource,
        .capability_generation = self.keyboard_capability_generation,
    };
    self.keyboard_resources.append(self.allocator, entry) catch {
        resource.postNoMemory();
        resource.destroy();
        return;
    };
    resource.setHandler(*Self, handleKeyboardRequest, handleKeyboardDestroy, self);
    self.notifySeatResourceCount();
    if (!self.keyboardResourceActive(entry)) return;
    if (self.keymap == null) return;
    self.sendKeymap(resource);
    self.sendRepeatInfo(resource);
    const surface = self.keyboardDeliverySurface() orelse return;
    if (self.parent_focused and resource.getClient() == surface.getClient()) {
        const serial = self.display.nextSerial();
        self.recordSelectionSerial(surface.getClient(), serial);
        self.sendEnterTo(resource, surface, serial);
    }
}

fn createPointer(self: *Self, seat: *wl.Seat, id: u32) void {
    const resource = wl.Pointer.create(seat.getClient(), seat.getVersion(), id) catch {
        seat.postNoMemory();
        return;
    };
    const generation = self.next_pointer_resource_generation;
    const entry: PointerResource = .{
        .resource = resource,
        .generation = generation,
        .capability_generation = self.pointer_capability_generation,
    };
    self.pointer_resources.append(self.allocator, entry) catch {
        resource.postNoMemory();
        resource.destroy();
        return;
    };
    self.next_pointer_resource_generation = std.math.add(u64, generation, 1) catch unreachable;
    resource.setHandler(*Self, handlePointerRequest, handlePointerDestroy, self);
    self.notifySeatResourceCount();
    if (!self.pointerResourceActive(entry)) return;
    const surface = self.pointerSurface() orelse return;
    if (resource.getClient() == surface.getClient()) {
        const serial = self.display.nextSerial();
        self.latest_pointer_enter = .{
            .client = surface.getClient(),
            .serial = serial,
        };
        self.sendPointerEnterTo(resource, surface, serial);
        if (resource.getVersion() >= wl.Pointer.frame_since_version) resource.sendFrame();
    }
}

fn createTouch(self: *Self, seat: *wl.Seat, id: u32) void {
    const resource = wl.Touch.create(seat.getClient(), seat.getVersion(), id) catch {
        seat.postNoMemory();
        return;
    };
    const generation = self.next_touch_resource_generation;
    self.touch_resources.append(self.allocator, .{
        .resource = resource,
        .generation = generation,
        .capability_generation = self.touch_capability_generation,
    }) catch {
        resource.postNoMemory();
        resource.destroy();
        return;
    };
    self.next_touch_resource_generation = std.math.add(u64, generation, 1) catch unreachable;
    resource.setHandler(*Self, handleTouchRequest, handleTouchDestroy, self);
    self.notifySeatResourceCount();
}

fn handleKeyboardRequest(resource: *wl.Keyboard, request: wl.Keyboard.Request, _: *Self) void {
    switch (request) {
        .release => resource.destroy(),
    }
}

fn handleKeyboardDestroy(resource: *wl.Keyboard, self: *Self) void {
    for (self.keyboard_resources.items, 0..) |candidate, index| {
        if (candidate.resource != resource) continue;
        _ = self.keyboard_resources.orderedRemove(index);
        self.notifySeatResourceCount();
        return;
    }
    unreachable;
}

fn handlePointerRequest(resource: *wl.Pointer, request: wl.Pointer.Request, self: *Self) void {
    switch (request) {
        .set_cursor => |set| if (self.pointerResourceIsActive(resource)) self.setCursor(
            resource,
            set.serial,
            set.surface,
            set.hotspot_x,
            set.hotspot_y,
        ),
        .release => resource.destroy(),
    }
}

fn handlePointerDestroy(resource: *wl.Pointer, self: *Self) void {
    for (self.pointer_resources.items, 0..) |candidate, index| {
        if (candidate.resource != resource) continue;
        _ = self.pointer_resources.orderedRemove(index);
        self.notifySeatResourceCount();
        return;
    }
    unreachable;
}

fn handleTouchRequest(resource: *wl.Touch, request: wl.Touch.Request, _: *Self) void {
    switch (request) {
        .release => resource.destroy(),
    }
}

fn handleTouchDestroy(resource: *wl.Touch, self: *Self) void {
    const client = resource.getClient();
    for (self.touch_frame_resources.items, 0..) |candidate, index| {
        if (candidate != resource) continue;
        _ = self.touch_frame_resources.orderedRemove(index);
        break;
    }
    for (self.touch_resources.items, 0..) |candidate, index| {
        if (candidate.resource != resource) continue;
        _ = self.touch_resources.orderedRemove(index);
        var client_has_resource = false;
        for (self.touch_resources.items) |remaining| {
            if (remaining.resource.getClient() == client) {
                client_has_resource = true;
                break;
            }
        }
        if (!client_has_resource) {
            for (self.touch_points.items) |*point| {
                const target = point.target orelse continue;
                if (target.client == client) point.target = null;
            }
        }
        self.notifySeatResourceCount();
        return;
    }
    unreachable;
}

fn focusedSurface(self: *Self) ?*wl.Surface {
    return Surface.resourceFor(self.surface_store, self.focus orelse return null);
}

fn keyboardDeliverySurfaceId(self: *const Self) ?Surface.Id {
    if (self.keyboard_grab) |grab| if (grab.surface) |surface_id| return surface_id;
    return self.focus;
}

fn keyboardDeliverySurface(self: *Self) ?*wl.Surface {
    return Surface.resourceFor(
        self.surface_store,
        self.keyboardDeliverySurfaceId() orelse return null,
    );
}

fn hasKeyboardCapability(self: *const Self) bool {
    return (self.keyboard_available or self.virtual_keyboard_count > 0) and self.keymap != null;
}

fn keyboardResourceActive(self: *const Self, entry: KeyboardResource) bool {
    return capabilityResourceActive(
        self.hasKeyboardCapability(),
        self.keyboard_capability_generation,
        entry.capability_generation,
    );
}

fn pointerResourceActive(self: *const Self, entry: PointerResource) bool {
    return capabilityResourceActive(
        self.pointer_available,
        self.pointer_capability_generation,
        entry.capability_generation,
    );
}

fn pointerResourceIsActive(self: *const Self, resource: *wl.Pointer) bool {
    const handle = self.pointerHandle(resource) orelse return false;
    return self.pointerHandleIsActive(handle);
}

fn touchResourceActive(self: *const Self, entry: TouchResource) bool {
    return capabilityResourceActive(
        self.touch_available,
        self.touch_capability_generation,
        entry.capability_generation,
    );
}

fn beginCapabilityGeneration(generation: *u64, ever_available: *bool) void {
    generation.* = std.math.add(u64, generation.*, 1) catch unreachable;
    ever_available.* = true;
}

fn capabilityResourceActive(available: bool, current: u64, resource: u64) bool {
    return available and resource == current;
}

fn findTouchPoint(self: *const Self, id: i32) ?usize {
    for (self.touch_points.items, 0..) |point, index| {
        if (point.id == id) return index;
    }
    return null;
}

fn touchPoint(self: *const Self, id: i32) ?*const TouchPoint {
    return &self.touch_points.items[self.findTouchPoint(id) orelse return null];
}

fn latestTouchResourceGeneration(self: *const Self, client: *wl.Client) ?u64 {
    var latest: ?u64 = null;
    for (self.touch_resources.items) |entry| {
        if (!self.touchResourceActive(entry)) continue;
        if (entry.resource.getClient() == client) latest = entry.generation;
    }
    return latest;
}

fn hasTouchResourceVersion(
    self: *const Self,
    client: *wl.Client,
    version: u32,
    max_generation: u64,
) bool {
    for (self.touch_resources.items) |entry| {
        if (self.touchResourceActive(entry) and
            touchResourceInSequence(entry.generation, max_generation) and
            entry.resource.getClient() == client and
            entry.resource.getVersion() >= version) return true;
    }
    return false;
}

fn markTouchFrame(self: *Self, resource: *wl.Touch) error{OutOfMemory}!void {
    for (self.touch_frame_resources.items) |pending| {
        if (pending == resource) return;
    }
    try self.touch_frame_resources.append(self.allocator, resource);
}

fn touchResourceInSequence(generation: u64, max_generation: u64) bool {
    return generation <= max_generation;
}

fn capabilities(self: *const Self) wl.Seat.Capability {
    return .{
        .keyboard = self.hasKeyboardCapability(),
        .pointer = self.pointer_available,
        .touch = self.touch_available,
    };
}

fn broadcastCapabilities(self: *Self) void {
    for (self.seat_resources.items) |resource| resource.sendCapabilities(self.capabilities());
}

fn sendKeymap(self: *Self, resource: *wl.Keyboard) void {
    const keymap = self.keymap orelse return;
    resource.sendKeymap(keymap.format, keymap.file.handle, keymap.size);
}

fn sendRepeatInfo(self: *const Self, resource: *wl.Keyboard) void {
    if (resource.getVersion() >= wl.Keyboard.repeat_info_since_version) {
        resource.sendRepeatInfo(self.repeat_info.rate, self.repeat_info.delay);
    }
}

fn sendEnter(self: *Self) void {
    if (self.keymap == null) return;
    const surface = self.keyboardDeliverySurface() orelse return;
    const serial = self.display.nextSerial();
    self.recordSelectionSerial(surface.getClient(), serial);
    for (self.keyboard_resources.items) |entry| {
        if (!self.keyboardResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient()) {
            self.sendEnterTo(resource, surface, serial);
        }
    }
}

fn sendEnterTo(self: *Self, resource: *wl.Keyboard, surface: *wl.Surface, serial: u32) void {
    var keys = wl.Array.fromArrayList(u32, self.pressed_keys);
    resource.sendEnter(serial, surface, &keys);
    self.sendModifiers(resource, serial);
}

fn sendLeave(self: *Self) void {
    const surface = self.keyboardDeliverySurface() orelse return;
    const serial = self.display.nextSerial();
    for (self.keyboard_resources.items) |entry| {
        if (!self.keyboardResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient()) resource.sendLeave(serial, surface);
    }
}

fn sendModifiers(self: *const Self, resource: *wl.Keyboard, serial: u32) void {
    resource.sendModifiers(
        serial,
        self.modifiers.depressed,
        self.modifiers.latched,
        self.modifiers.locked,
        self.modifiers.group,
    );
}

fn updatePointerFocus(self: *Self, focus: ?PointerFocus, motion_time: ?u32) void {
    const changed = if (self.pointer_focus) |current|
        if (focus) |next| !std.meta.eql(current.surface_id, next.surface_id) else true
    else
        focus != null;
    if (changed) {
        self.clearCursor();
        self.sendPointerLeave();
        self.pointer_focus = focus;
        self.latest_pointer_enter = null;
        self.sendPointerEnter();
        if (focus == null) self.restoreControllerCursor();
        return;
    }
    self.pointer_focus = focus;
    const time = motion_time orelse return;
    const surface = self.pointerSurface() orelse return;
    const position = self.pointer_focus orelse return;
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient()) {
            resource.sendMotion(time, fixed(position.x), fixed(position.y));
        }
    }
}

fn pointerSurface(self: *Self) ?*wl.Surface {
    const focus = self.pointer_focus orelse return null;
    return Surface.resourceFor(self.surface_store, focus.surface_id);
}

fn sendPointerEnter(self: *Self) void {
    const surface = self.pointerSurface() orelse return;
    const serial = self.display.nextSerial();
    self.latest_pointer_enter = .{
        .client = surface.getClient(),
        .serial = serial,
    };
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() == surface.getClient()) {
            self.sendPointerEnterTo(resource, surface, serial);
        }
    }
}

fn sendPointerEnterTo(
    self: *const Self,
    resource: *wl.Pointer,
    surface: *wl.Surface,
    serial: u32,
) void {
    const position = self.pointer_focus orelse return;
    resource.sendEnter(serial, surface, fixed(position.x), fixed(position.y));
}

fn sendPointerLeave(self: *Self) void {
    const surface = self.pointerSurface() orelse return;
    const serial = self.display.nextSerial();
    for (self.pointer_resources.items) |entry| {
        if (!self.pointerResourceActive(entry)) continue;
        const resource = entry.resource;
        if (resource.getClient() != surface.getClient()) continue;
        resource.sendLeave(serial, surface);
        if (resource.getVersion() >= wl.Pointer.frame_since_version) resource.sendFrame();
    }
}

fn setPointerPosition(self: *Self, x: f64, y: f64) void {
    std.debug.assert(std.math.isFinite(x) and std.math.isFinite(y));
    const old_cursor = self.cursorInfo();
    self.pointer_position = .{ .x = x, .y = y };
    const new_cursor = self.cursorInfo();
    if (self.repaint_listener) |listener| {
        if (old_cursor) |old| {
            const new = new_cursor orelse unreachable;
            listener.cursor_moved(listener.context, old, new);
        } else if (new_cursor) |new| {
            listener.cursor_moved(listener.context, new, new);
        }
    }
}

fn setCursor(
    self: *Self,
    pointer: *wl.Pointer,
    serial: u32,
    surface_resource: ?*wl.Surface,
    hotspot_x: i32,
    hotspot_y: i32,
) void {
    const cursor_surface = if (surface_resource) |resource| cursor: {
        const surface = Surface.fromResource(resource);
        if (surface.assignedRole()) |role| {
            if (role != .cursor or !CursorSurface.ownedBy(surface, self)) {
                pointer.postError(.role, "wl_surface already has another role");
                return;
            }
        } else {
            CursorSurface.create(self, surface) catch |err| switch (err) {
                error.OutOfMemory => {
                    pointer.postNoMemory();
                    return;
                },
                error.RoleUnavailable => {
                    pointer.postError(.role, "wl_surface is unavailable for the cursor role");
                    return;
                },
            };
        }
        break :cursor surface;
    } else null;

    const manager_controller = self.isUnfocusedCursorController(pointer.getClient());
    const drag_controller = if (self.drag_cursor_client) |client|
        client == pointer.getClient()
    else
        false;
    const controller = manager_controller or drag_controller;
    const enter = self.latest_pointer_enter;
    if (!controller and (enter == null or enter.?.client != pointer.getClient() or enter.?.serial != serial)) return;
    const focused_client = if (self.pointerSurface()) |surface|
        surface.getClient() == pointer.getClient()
    else
        false;
    if (!controller and !focused_client and !self.activeCursorOwnedBy(pointer.getClient())) return;

    const requested: ?ActiveCursor = if (cursor_surface) |surface| .{ .surface = .{
        .surface_id = surface.handle(),
        .hotspot_x = hotspot_x,
        .hotspot_y = hotspot_y,
    } } else null;
    if (manager_controller and !drag_controller) {
        self.cursor_controller.?.cursor = requested;
        self.cursor_controller.?.configured = true;
        if (self.pointer_focus) |focus| {
            const focused_surface = self.surface_store.get(focus.surface_id);
            if (focused_surface == null or focused_surface.?.resource.getClient() != pointer.getClient()) return;
        }
    }
    self.active_cursor = requested;
    self.requestRepaint();
}

pub fn setCursorShape(
    self: *Self,
    client: *wl.Client,
    serial: u32,
    shape: ShapeCursor,
) void {
    std.debug.assert(shape.client == client);
    const manager_controller = self.isUnfocusedCursorController(client);
    const drag_controller = if (self.drag_cursor_client) |drag_client|
        drag_client == client
    else
        false;
    const controller = manager_controller or drag_controller;
    const enter = self.latest_pointer_enter;
    if (!controller and (enter == null or enter.?.client != client or enter.?.serial != serial)) return;
    const focused_client = if (self.pointerSurface()) |surface|
        surface.getClient() == client
    else
        false;
    if (!controller and !focused_client and !self.activeCursorOwnedBy(client)) return;

    const requested: ActiveCursor = .{ .shape = shape };
    if (manager_controller and !drag_controller) {
        self.cursor_controller.?.cursor = requested;
        self.cursor_controller.?.configured = true;
        if (self.pointer_focus) |focus| {
            const focused_surface = self.surface_store.get(focus.surface_id);
            if (focused_surface == null or focused_surface.?.resource.getClient() != client) return;
        }
    }
    self.active_cursor = requested;
    self.requestRepaint();
}

pub fn clearCursorShapes(self: *Self) void {
    self.default_cursor = null;
    if (self.active_cursor) |cursor| switch (cursor) {
        .surface => {},
        .shape => self.clearCursor(),
    };
    if (self.cursor_controller) |*controller| if (controller.cursor) |cursor| switch (cursor) {
        .surface => {},
        .shape => controller.cursor = null,
    };
}

fn activeCursorOwnedBy(self: *Self, client: *wl.Client) bool {
    const cursor = self.active_cursor orelse return false;
    return switch (cursor) {
        .surface => |surface| if (Surface.resourceFor(self.surface_store, surface.surface_id)) |resource|
            resource.getClient() == client
        else
            false,
        .shape => |shape| shape.client == client,
    };
}

fn restoreControllerCursor(self: *Self) void {
    self.active_cursor = if (self.cursor_controller) |controller| controller.cursor else null;
    self.requestRepaint();
}

fn clearCursor(self: *Self) void {
    if (self.active_cursor == null) return;
    self.active_cursor = null;
    self.requestRepaint();
}

fn cursorSurfaceCommitted(self: *Self, id: Surface.Id, info: Surface.CommitInfo) void {
    var repaint = false;
    if (self.active_cursor) |*cursor| switch (cursor.*) {
        .shape => {},
        .surface => |*surface| if (std.meta.eql(surface.surface_id, id)) {
            surface.hotspot_x -|= info.offset_x;
            surface.hotspot_y -|= info.offset_y;
            repaint = true;
        },
    };
    if (self.cursor_controller) |*controller| if (controller.cursor) |*remembered| switch (remembered.*) {
        .shape => {},
        .surface => |*surface| if (std.meta.eql(surface.surface_id, id)) {
            surface.hotspot_x -|= info.offset_x;
            surface.hotspot_y -|= info.offset_y;
        },
    };
    if (repaint) self.requestRepaint();
}

fn cursorSurfaceDestroyed(self: *Self, id: Surface.Id) void {
    if (self.active_cursor) |cursor| {
        switch (cursor) {
            .shape => {},
            .surface => |surface| {
                if (std.meta.eql(surface.surface_id, id)) self.clearCursor();
            },
        }
    }
    if (self.cursor_controller) |*controller| if (controller.cursor) |cursor| {
        switch (cursor) {
            .shape => {},
            .surface => |surface| {
                if (std.meta.eql(surface.surface_id, id)) controller.cursor = null;
            },
        }
    };
}

fn requestRepaint(self: *Self) void {
    if (self.repaint_listener) |listener| listener.request(listener.context);
}

fn recordUserAction(self: *Self, client: *wl.Client, serial: u32) void {
    const action: UserAction = .{ .client = client, .serial = serial };
    self.last_user_action = action;
    self.recordSelectionSerial(client, serial);
}

fn recordSelectionSerial(self: *Self, client: *wl.Client, serial: u32) void {
    const action: UserAction = .{ .client = client, .serial = serial };
    self.recent_user_actions[self.next_user_action] = action;
    self.next_user_action = (self.next_user_action + 1) % user_action_history_capacity;
    self.recent_user_action_count = @min(
        self.recent_user_action_count + 1,
        user_action_history_capacity,
    );
}

fn notifyKeyboardFocus(self: *Self) void {
    for (self.keyboard_focus_listeners.items) |listener| {
        listener.changed(listener.context, self.keyboardFocusedClient());
    }
}

const CursorSurface = struct {
    seat: *Self,
    surface_id: Surface.Id,

    fn create(seat: *Self, surface: *Surface) error{ OutOfMemory, RoleUnavailable }!void {
        const self = seat.allocator.create(CursorSurface) catch return error.OutOfMemory;
        errdefer seat.allocator.destroy(self);
        self.* = .{
            .seat = seat,
            .surface_id = surface.handle(),
        };
        surface.reserveRole(.cursor, .{
            .context = self,
            .before_commit = beforeCommit,
            .after_commit = afterCommit,
            .surface_destroyed = surfaceDestroyed,
            .role_tag = .pointer_cursor,
        }) catch return error.RoleUnavailable;
        errdefer surface.releaseRole(self);
        surface.assignReservedRole(.cursor, self) catch return error.RoleUnavailable;
        seat.cursor_surface_count += 1;
    }

    fn ownedBy(surface: *Surface, seat: *Self) bool {
        const identity = surface.roleIdentity(.cursor) orelse return false;
        if (identity.tag != .pointer_cursor) return false;
        const cursor_surface: *CursorSurface = @ptrCast(@alignCast(identity.context));
        return cursor_surface.seat == seat;
    }

    fn beforeCommit(_: *anyopaque, _: Surface.CommitInfo) Surface.CommitAction {
        return .apply;
    }

    fn afterCommit(context: *anyopaque, info: Surface.CommitInfo) void {
        const self: *CursorSurface = @ptrCast(@alignCast(context));
        self.seat.cursorSurfaceCommitted(self.surface_id, info);
    }

    fn surfaceDestroyed(context: *anyopaque) void {
        const self: *CursorSurface = @ptrCast(@alignCast(context));
        const seat = self.seat;
        seat.cursorSurfaceDestroyed(self.surface_id);
        std.debug.assert(seat.cursor_surface_count > 0);
        seat.cursor_surface_count -= 1;
        seat.allocator.destroy(self);
    }
};

pub fn cursorCoordinate(value: f64, hotspot: i32) i32 {
    const coordinate: i64 = @intFromFloat(@floor(value));
    return @intCast(std.math.clamp(
        coordinate - @as(i64, hotspot),
        std.math.minInt(i32),
        std.math.maxInt(i32),
    ));
}

fn fixed(value: f64) wl.Fixed {
    const minimum = @as(f64, @floatFromInt(std.math.minInt(i32))) / 256.0;
    const maximum = @as(f64, @floatFromInt(std.math.maxInt(i32))) / 256.0;
    return wl.Fixed.fromDouble(std.math.clamp(value, minimum, maximum));
}

test "cursor position accounts for hotspot and fractional motion" {
    try std.testing.expectEqual(@as(i32, 8), cursorCoordinate(12.75, 4));
    try std.testing.expectEqual(@as(i32, -5), cursorCoordinate(0.25, 5));
    try std.testing.expectEqual(
        std.math.minInt(i32),
        cursorCoordinate(-0.25, std.math.maxInt(i32)),
    );
}

test "protocol serial ordering handles wraparound" {
    try std.testing.expect(!serialIsOlder(10, 10));
    try std.testing.expect(!serialIsOlder(11, 10));
    try std.testing.expect(serialIsOlder(9, 10));
    try std.testing.expect(!serialIsOlder(1, std.math.maxInt(u32)));
    try std.testing.expect(serialIsOlder(std.math.maxInt(u32), 1));
}

test "seat capability generations invalidate existing input resources" {
    var generation: u64 = 0;
    var ever_available = false;

    beginCapabilityGeneration(&generation, &ever_available);
    try std.testing.expect(ever_available);
    try std.testing.expect(capabilityResourceActive(true, generation, generation));
    try std.testing.expect(!capabilityResourceActive(false, generation, generation));

    const previous = generation;
    beginCapabilityGeneration(&generation, &ever_available);
    try std.testing.expect(!capabilityResourceActive(true, generation, previous));
    try std.testing.expect(capabilityResourceActive(true, generation, generation));
}

test "touch resources bound after down do not join the contact sequence" {
    try std.testing.expect(touchResourceInSequence(4, 4));
    try std.testing.expect(!touchResourceInSequence(5, 4));
}
